#
# A theme based on steeef theme
#  * RVM/Rbenv info shown on the right
#  * Git branch info on the left
#  * Single line prompt
#
# Authors:
#   Adrian Castillo <rodacato@gmail.com>

function prompt_rodacato_precmd {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS

  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info on
    git-info
  fi

  # Get ruby information
  if (( $+functions[ruby-info] )); then
    ruby-info
  fi
}

function prompt_rodacato_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  autoload -Uz add-zsh-hook

  add-zsh-hook precmd prompt_rodacato_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    __PROMPT_RODACATO_COLORS=(
      "%F{81}"  # turquoise
      "%F{166}" # orange
      "%F{135}" # purple
      "%F{161}" # hotpink
      "%F{118}" # limegreen
    )
  else
    __PROMPT_RODACATO_COLORS=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # git
  zstyle ':prezto:module:git:info:branch' format "${__PROMPT_RODACATO_COLORS[1]}%b%f"
  zstyle ':prezto:module:git:info:added' format "${__PROMPT_RODACATO_COLORS[5]}✚%f"
  zstyle ':prezto:module:git:info:deleted' format "${__PROMPT_RODACATO_COLORS[2]}✖%f"
  zstyle ':prezto:module:git:info:modified' format "${__PROMPT_RODACATO_COLORS[4]}✱%f"
  zstyle ':prezto:module:git:info:untracked' format "${__PROMPT_RODACATO_COLORS[3]}◼%f"
  zstyle ':prezto:module:git:info:ahead' format "${__PROMPT_RODACATO_COLORS[5]}⬆%f%"
  zstyle ':prezto:module:git:info:behind' format "${__PROMPT_RODACATO_COLORS[2]}⬇%f%"
  zstyle ':prezto:module:git:info:remote' format "${__PROMPT_RODACATO_COLORS[1]}%R%f"
  zstyle ':prezto:module:git:info:keys' format 'prompt' '(%d%a%m%u%A%B)' \
    'rprompt' '%A%B'

  # ruby info (rvm, rbenv)
  zstyle ':prezto:module:ruby:info:version' format '[%v]'

  PROMPT="${__PROMPT_RODACATO_COLORS[3]}%n%f@${__PROMPT_RODACATO_COLORS[2]}%m%f ${__PROMPT_RODACATO_COLORS[5]}%~%f "'$git_info[prompt]'"$ "
  RPROMPT='%F{blue}${ruby_info[version]}'
}

prompt_rodacato_setup "$@"
